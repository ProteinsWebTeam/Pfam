
# AlignmentGenerator.pm
# jt6 20060601 WTSI
#
# $Id: AlignmentGenerator.pm,v 1.16 2007-06-28 13:33:34 jt6 Exp $

=head1 NAME

PfamWeb::Controller::Family::AlignmentGenerator - viewing sequence alignments

=cut

package PfamWeb::Controller::Family::AlignmentGenerator;

=head1 DESCRIPTION

Various methods for viewing alignments.

$Id: AlignmentGenerator.pm,v 1.16 2007-06-28 13:33:34 jt6 Exp $

=cut

use strict;
use warnings;

use Data::Dump qw( dump );
use Data::Pageset;
use Bio::Pfam::ColourAlign;

use base "PfamWeb::Controller::Family";

#-------------------------------------------------------------------------------

=head1 METHODS

=head2 default : Private

Generates a segment of a sequence alignment. Retrieves the alignment
from DAS sources and determines start/end row numbers from parameters.

Picks up a URL like http://localhost:3000/family/alignmentgenerator?acc=PF00067

This action shouldn't really be called directly, but via the page
generated by C<showAlignment>.

=cut

sub default : Private {
  my( $this, $c ) = @_;

  return unless defined $c->stash->{pfam};

  # somewhere to dump the paging info
  $c->stash->{alignments} = {};

  # retrieve the DasLite client from the base model class and hand it
  # the DSN
  my $dl = $c->model('PfamDB')->getDasLite;
  my $numRowsInAlignment;
  if( $c->req->param('alnType') and
      $c->req->param('alnType') eq 'seed' ) {
    $dl->dsn( [ qw|http://das.sanger.ac.uk/das/pfamSeedAlign| ] );
    $numRowsInAlignment = $c->stash->{pfam}->num_seed;
  } else {
    $dl->dsn( [ qw|http://das.sanger.ac.uk/das/pfamFullAlign| ] );
    $numRowsInAlignment = $c->stash->{pfam}->num_full;
  }
  $c->log->debug( "Family::AlignmentGenerator::default: |$numRowsInAlignment| rows in alignment" );

  # get the scroll position
  $c->stash->{scrollValue} = $c->req->param('scrollValue' ) || 0 =~ m/^(\d+)$/;
  $c->log->debug( "Family::AlignmentGenerator::default: set scroll value to |"
                  . $c->stash->{scrollValue} . "|" );

  # get the number of alignment lines to display
  my( $numRowsToShow ) =
    $c->req->param('numRows') || $this->{defaultRows} =~ m/^(\d+)$/;
  $c->log->debug( "Family::AlignmentGenerator::default: showing |$numRowsToShow| rows" );

  # build a Data::Pageset object to help keep track of all this...
  my $pager = Data::Pageset->new( { total_entries    => $numRowsInAlignment, 
                                    entries_per_page => $numRowsToShow,
                                    pages_per_set    => 11,
                                    mode             => 'slide' } );
  $c->stash->{pager} = $pager;

  # find out what page we want for this request
  my $page = 1;
  
  if( defined $c->req->param('page') ) {
    ( $page ) = $c->req->param('page') =~ m/^(\d+)$/;
    $c->log->debug( "Family::AlignmentGenerator::default: requested page number |$page|" );
  } elsif( defined $c->req->param('next') ) {
    $page = $pager->next;
    $c->log->debug( 'Family::AlignmentGenerator::default: requested next page' );
  } elsif( defined $c->req->param('prev') ) {
    $page = $pager->prev;
    $c->log->debug( 'Family::AlignmentGenerator::default: requested previous page' );
  }
  $c->log->debug( "Family::AlignmentGenerator::default: showing page |$page|" );
  $page ||= 1;
  
  $pager->current_page( $page ); 

  # decide which actual rows we need to use now
  my $rows = $pager->first . "-" . $pager->last;
  $c->log->debug( "Family::AlignmentGenerator::default: rows: |$rows|" );

  # store the lists of page numbers before and after the current one, for use
  # by the template in generating the list of available pages
  $c->stash->{pagesBefore} = [         1 .. $page - 1         ] if $page > 1;
  $c->stash->{pagesAfter}  = [ $page + 1 .. $pager->last_page ] if $page < $pager->last_page;
  
  # retrieve the raw alignment fragment and associated features via DAS and 
  # generate the consensus sequence 
  my $rawAlignment = $dl->alignment( { query => $c->stash->{acc},
                                       rows  => $rows } );
  my $features     = $dl->features( $c->stash->{acc} );
  my $consensus    = Bio::Pfam::ColourAlign::parseConsensus( getConsensus( $features ) );
#  $c->log->debug( "rawAlignment: " . dump $rawAlignment );
#  $c->log->debug( "features:     " . dump $features );

  # build the marked-up alignment
  my( $alignments, $alignmentLengths ) = reconstructAli( $rawAlignment );
  my @markedUpAlignments;
  foreach my $alignment ( @$alignments ) {
    push @markedUpAlignments, 
      Bio::Pfam::ColourAlign::markupAlignSeparate( $alignment, $consensus );
  }

  # stash the stuff that we built
  $c->stash->{alignments}->{alignments} = \@markedUpAlignments;
  $c->stash->{alignments}->{lengths}    = $alignmentLengths;

  # hand off to the template
  $c->stash->{template} = "components/blocks/family/alignmentFragment.tt";
}

#-------------------------------------------------------------------------------

=head2 showAlignment : Path

Hands straight off to a template that generates a "tool" page
containing the necessary hooks to load the interactive alignment
viewer.

Picks up a URL like http://localhost:3000/family/alignmentviewer?acc=PF00067

=cut

sub showAlignment : Path( "/family/alignmentviewer" ) {
  my( $this, $c ) = @_;

  $c->stash->{template} = "components/blocks/family/alignmentTool.tt";
}

#-------------------------------------------------------------------------------

=head2 showJalview : Path

Hands straight off to a template that generates a "tool" page
containing the JalView applet.

Picks up a URL like http://localhost:3000/family/jalview?acc=PF00067

=cut

sub showJalview : Path( "/family/jalview" ) {
  my( $this, $c ) = @_;

  $c->stash->{template} = "components/tools/jalview.tt";
}

#-------------------------------------------------------------------------------
#- private methods -------------------------------------------------------------
#-------------------------------------------------------------------------------

# reconstructs a blocked alignment from a raw alignment

sub reconstructAli {
  my $ali = shift;
  my ($source, $aliData) = each %$ali;

  my( @alignments, @alignmentLengths );

  for( my $i = 0; $i < scalar(@$aliData); $i++ ) {
    my %aliObjects = 
      map{ $_->{alignobject_intObjectId} => $_ } @{ $aliData->[$i]->{alignobject} };
  
    push @alignmentLengths, $aliData->[$i]->{alignment_max};
  
    foreach my $block ( sort { $a->{block_blockOrder} <=> $b->{block_blockOrder} }
                             @{$aliData->[$i]->{block} } ) {
      my %ali;
      foreach my $bseqRef (@{ $block->{segment} } ) {
  
        my $key = $bseqRef->{segment_intObjectId} . "/" . 
                  $bseqRef->{segment_start}       . "-" . 
                  $bseqRef->{segment_end};
    
        $ali{$key} = &getAliString($bseqRef, \%aliObjects);
      }
      push @alignments, \%ali;
    }
  }
  return \@alignments, \@alignmentLengths;
}

#-------------------------------------------------------------------------------
# get the alignment string from the alignment

sub getAliString{
  my( $bseqRef, $aliObjectsRef ) = @_;

  my $seqStr = $aliObjectsRef->{ $bseqRef->{segment_intObjectId} }->{sequence};

  my $seq = substr( $seqStr,
                    $bseqRef->{segment_start} - 1,
                    $bseqRef->{segment_end} - $bseqRef->{segment_start} + 1 );

  my $aliString = cigar2align( $bseqRef->{cigar}, $seq );

  return $aliString;
}

#-------------------------------------------------------------------------------
# convert a cigar string into an alignment row

sub cigar2align  {
  my $cigar = shift;

  $cigar =~ s/\"//g;

  my $seq = shift;
  my $tmp = $cigar;
  my $start = 0;
  my $len = length($seq);

  $tmp =~ s/(\d+)D/'-'x$1/eg;
  $tmp =~ s/D/\-/g;
  $tmp =~ s/(\d+)I/'.'x$1/eg;
  $tmp =~ s/I/\./g;

  $tmp =~ s/(\d{0,5})M/if($1){$start+=$1,($start<=$len)?substr($seq,$start-$1,$1):'~'x$1}else{$start+=1,($start<=$len)?substr($seq,$start-1,1):'~'}/eg;

  return $tmp;
}

#-------------------------------------------------------------------------------
# calculate the consensus sequence

sub getConsensus {
  my $con = shift;

  my( $source, $featuresRef ) = each %$con;
  return $featuresRef->[0]->{feature_label};
}

#-------------------------------------------------------------------------------

=head1 AUTHOR

John Tate, C<jt6@sanger.ac.uk>

Rob Finn, C<rdf@sanger.ac.uk>

=head1 COPYRIGHT

Copyright (c) 2007: Genome Research Ltd.

Authors: Rob Finn (rdf@sanger.ac.uk), John Tate (jt6@sanger.ac.uk)

This is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public License
as published by the Free Software Foundation; either version 2
of the License, or (at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
or see the on-line version at http://www.gnu.org/copyleft/gpl.txt

=cut

1;
